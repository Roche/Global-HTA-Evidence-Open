# Fractional polynomial, 2nd order, random effects model: a RE is put only on the scale parameter 
#                                                          i.e. on the intercept in the frac poly
#                                                         a log-normal prior is used for the RE Var
#                                                           similar to the informative Turner priors
#                                                           using the standard parameterization via meanlog, sdlog
# -------------------------------------------------------------------------------
# Data: grouped survival data, binomial likelihood, linear predictor on log-hazard
#         Nobs     number of observations
#         n[i]     patients at risk in interval i
#         r[i]     events during interval i
#         time[i]  mid-point of interval i
#         dt[i]    length of interval i
#         Ns       number of studies
#         Na[j]    number of arms in study j
#         Ntrt     number of treatments
#         s[i]     study number for obs i
#         a[i]     arm number (within study) for obs i
#         t[i,j]   treatment in study i arm j
#         P1       exponent of the time varying term in the fractional polynomial
#         feprior_mean[1:3]         prior mean (for contrasts d and baselines mu)
#         feprior_prec[,]           prior precision (for d and mu)
#         reprior_meanlog           Y~LN(meanlog, sdlog) then log(Y)~N(meanlog, sdlog^2)
#         reprior_sdlog           
# -------------------------------------------------------------------------------

model{

## Sampling model (likelihood)
for (i in 1:Nobs){
  time1[i] <- (equals(P1, 0) * log(time[i]) + (1 - equals(P1, 0)) * pow(time[i], P1)) 
  time2[i] <- ((1 - equals(P2, P1)) * (equals(P2, 0) * log(time[i]) + (1 - equals(P2, 0)) * pow(time[i], P2)) + 
                  equals(P2, P1) * (equals(P2, 0) * log(time[i]) * log(time[i]) + (1 - equals(P2, 0)) * pow(time[i], P2) * log(time[i])))


  # likelihood: digitized KM curves
  r[i] ~ dbin(p[i], n[i])
  p[i] <- 1 - exp(-h[i] * dt[i])  # cumulative hazard over interval [t,t+dt] expressed as deaths per person-month

  # fractional polynomial
  log(h[i]) <- Beta[s[i], a[i], 1] + Beta[s[i], a[i], 2] * time1[i] + Beta[s[i], a[i], 3] * time2[i]
  }


  ## Arm level parameters = study effect + trt effect (RE model, consistency eq for pop pars)
  for (i in 1:Ns){
    w[i, 1] <- 0
    delta[i, 1] <- 0

    for (j in 1:Na[i]){
      Beta[i, j, 1] <- mu[i, 1] + delta[i, j]
      Beta[i, j, 2] <- mu[i, 2] + d[t[i, j], 2] - d[t[i, 1], 2]
      Beta[i, j, 3] <- mu[i, 3] + d[t[i, j], 3] - d[t[i, 1], 3]
      }

    for (j in 2:Na[i]){
      delta[i, j] ~ dnorm(md[i, j], taud[i, j])
      md[i, j] <- d[t[i, j], 1] - d[t[i, 1], 1] + sw[i, j]
      w[i, j] <- (delta[i, j] - d[t[i, j], 1] + d[t[i, 1], 1])
      sw[i, j] <- sum(w[i, 1:(j - 1)]) / (j - 1) 
      taud[i, j] <- tau * 2 * (j - 1) / j 
      }
    
    }


     
## Priors
for (j in 1:Ns){
  mu[j, 1:3] ~ dmnorm(feprior_mean[1:3], feprior_prec[,]) 
  }

d[1, 1] <- 0
d[1, 2] <- 0
d[1, 3] <- 0
for (i in 2:Ntrt){
  d[i, 1:3] ~ dmnorm(feprior_mean[1:3], feprior_prec[,]) 
  }


reprior_preclog <- 1 / (reprior_sdlog * reprior_sdlog)
logVar ~ dnorm(reprior_meanlog, reprior_preclog) 
sd <- sqrt(exp(logVar))
tau <- 1 / (sd * sd)

} # end of model
